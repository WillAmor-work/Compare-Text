USE [BBLIVE]
GO
/****** Object:  StoredProcedure [dbo].[BB_IPOExport_EPO]    Script Date: 21/02/2024 18:27:09 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
-- PROCEDURE :	BB_IPOExport_EPO
-- VERSION :	1
-- DESCRIPTION: This SP returns a table filled with Upcoming/Recent EP Renewal Fee info.
--	
-- CALLED BY :	IPOExports SSIS package
--
-- Date			Who		Change	Version	Description
-- -----------	-------	------	-------	----------------------------------------------- 
-- 22/01/2024	WDA				1		Procedure created

ALTER   procedure [dbo].[BB_IPOExport_EPO]
AS

BEGIN
	select
		c.COUNTRYCODE as [Procedure],
		'EP' + LEFT(SUBSTRING(appno.OFFICIALNUMBER, PATINDEX('%[0-9.-]%', appno.OFFICIALNUMBER), 8000), PATINDEX('%[^0-9.-]%', SUBSTRING(appno.OFFICIALNUMBER, PATINDEX('%[0-9.-]%', appno.OFFICIALNUMBER), 8000) + 'X') -1) as [Application Number],
		[dbo].[fn_StripNonNumerics](regno.OFFICIALNUMBER) as [European patent No.],
		c.irn as [Reference], 
		--(select top 1 n.NAME from casename cn join name n on n.nameno = cn.nameno where cn.nametype = 'o' and cn.caseid = c.caseid order by cn.SEQUENCE) as [Applicant],
		[Applicant] = NULL,
		--CONVERT(DATE,nrd.NEXTRENEWALDATE) as [Due Date],
		[Due Date] = NULL,
		CASE c.COUNTRYCODE
			WHEN 'EP' Then IIF(fc.RATENO=99996, 30, 90)
			WHEN 'UP' Then IIF(fc.RATENO=99996, 730, 750)
		END + nrd.ANNUITY as [Fee Code],
		fc.DISBBASEFEE as [Amount],
		1 as [Number of Fees],
		fc.DISBBASEFEE as [Total],
		c.OFFICEID --leave in as SSIS will need to filter on Office
	from 
		cases c
	join 
		caseevent ce on ce.caseid = c.caseid 
		and ce.eventno = 1352
		and ce.eventdate between dateadd(day, -7, convert(date, getdate())) and convert(date, getdate())
	left join 
		OFFICIALNUMBERS appno on appno.caseid = c.caseid
		and appno.NUMBERTYPE = 'A'
	left join 
		OFFICIALNUMBERS regno on regno.caseid = c.caseid
		and regno.NUMBERTYPE = 'R'
	join 
		[dbo].[fn_GetNextRenewalDate] (0) nrd on nrd.caseid = c.caseid
	left join CASEEVENT ce199 on ce199.CASEID = c.CASEID
		and ce199.EVENTNO = -199
		and ce199.EVENTDATE is not null
		and ce199.CYCLE = nrd.CYCLE
	join 
		(
			select cr.countrycode, r.rateno, fc.* 
			from 
				criteria cr
			join 
				rates r	on r.rateno = cr.RATENO
			join 
				FEESCALCULATION fc on fc.criteriano = cr.criteriano
			where 
				cr.countrycode in ('EP', 'UP')
				and r.rateno in (99996, 100004)
				and cr.RULEINUSE = 1
		) fc on fc.CYCLENUMBER = nrd.ANNUITY
			and fc.COUNTRYCODE = c.COUNTRYCODE
			and (fc.RATENO = 99996 OR (fc.RATENO = 100004 and ce199.EVENTDATE is not null))
	
	where 
		c.countrycode in ('EP', 'UP')
		and c.casetype = 'A'
		and c.propertytype = 'P'

END
