/*******************************************************************************

                            CPA Software Solutions
                       Standard Approach to Conversion

                             Disable all triggers

InproTech version: 3.4.2

Description:
 This script will disable all triggers on all Inprotech tables.

Modification History:
 2009-01-16 AvB  3: Change table name.
 2007-10-04 AvB  2: Alter only tables, not views.
 2007-04-20 AvB  1: Exclude conversion table.
 2007-03-06 AvB     Created

*******************************************************************************/
USE EMEABB
GO

DECLARE @TableSchema sysname,
        @TableName   sysname,
        @SQLStr      nvarchar (1000),
        @Action      nvarchar (20),
        @ActionDesc  nvarchar (20),
        @NewLine     nchar (2)

SET @Action = 'DISABLE'
--SET @Action = 'ENABLE'

IF @Action = 'DISABLE'
  SET @ActionDesc = 'Disabling'
IF @Action = 'ENABLE'
  SET @ActionDesc = 'Enabling'

SET @NewLine = NCHAR (13) + NCHAR (10)

DECLARE InproTableCursor CURSOR FOR
  SELECT QUOTENAME (TABLE_SCHEMA),
         QUOTENAME (TABLE_NAME)
  FROM INFORMATION_SCHEMA.TABLES
  WHERE OBJECTPROPERTY (OBJECT_ID (QUOTENAME (TABLE_SCHEMA) + '.' + QUOTENAME (TABLE_NAME)), 'IsMSShipped') = 0 AND
        TABLE_NAME <> N'Conv_ScriptsToRun' AND --1:+ --2: --3:
        TABLE_TYPE  = N'BASE TABLE' --2:+

OPEN InproTableCursor

FETCH NEXT FROM InproTableCursor
  INTO @TableSchema,
       @TableName

WHILE @@FETCH_STATUS = 0
BEGIN

  SET @SQLStr = N'PRINT ''' + @ActionDesc + ' trigger(s) for ' + @TableSchema + N'.' + @TableName + N'...''' + @NewLine +
                N'ALTER TABLE ' + @TableSchema + N'.' + @TableName + N' ' + @Action + N' TRIGGER ALL'

  --PRINT @SQLStr
  EXECUTE sp_executesql @SQLStr

  FETCH NEXT FROM InproTableCursor
    INTO @TableSchema,
         @TableName

END -- while: fetch_status = 0

CLOSE InproTableCursor
DEALLOCATE InproTableCursor
GO

--
-- END OF SCRIPT
--
